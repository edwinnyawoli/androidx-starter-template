// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            'android': [
                    'compileSdk': 28,
                    'buildTools': '28.0.3',
                    'minSdk'    : 21,
                    'targetSdk' : 28,
            ],
            'tools'  : [
                    'gradle'   : '3.5.0',
                    'kotlin'   : '1.3.31',
                    'jacoco'   : '0.8.4',
                    'fabric'   : '1.29.0',
                    'safe_args': '1.0.0-alpha05',
                    'spotbugs' : '1.6.6'
            ],
            'deps'   : [
                    'androidx'            : [
                            'core'            : '2.1.0-beta01',
                            'supportLib'      : '1.1.0-beta01',
                            'constraintlayout': '1.1.3',
                            'exifinterface'   : '1.0.0',
                            'legacysupport'   : '1.0.0',
                            'navigation'      : '1.0.0-rc2',
                            'recyclerview'    : '1.1.0-alpha06',
                            'cardview'        : '1.0.0',
                            'workmanager'     : '2.0.1',
                            'paging'          : '2.1.0',
                            'room'            : '2.1.0',
                            'lifecycle'       : [
                                    'extensions': '2.2.0-alpha01',
                                    'viewmodel' : '2.1.0-alpha04',
                            ]
                    ],
                    'material'            : '1.1.0-alpha07',
                    'okhttp3'             : [
                            'logginginterceptor': '3.9.1'
                    ],
                    'kotlin'              : '1.2.51',
                    'retrofit'            : '2.5.0',
                    'timber'              : '4.5.1',
                    'dagger'              : '2.23.1',
                    'rx'                  : [
                            'java'   : '2.2.8',
                            'android': '2.1.0'
                    ],
                    'threetenabp'         : '1.0.5',
                    'mvpskeleton'         : 'v2.0.1',
                    'lombok'              : '1.18.8',
                    'permissiondispatcher': '4.3.1',
                    'multistateview'      : '1.3.2',
                    'crashlytics'         : '2.10.1',
                    'stetho'              : '1.5.0',
                    'test'                : [
                            'junit'       : '4.12',
                            'runner'      : '1.1.0-alpha4',
                            'espresso'    : '3.2.0-alpha03',
                            'mockito'     : '2.19.0',
                            'orchestrator': '1.0.2',
                            'roboelectric': '4.2.1',
                            'androidx'    : '1.2.0',
                            'androidxjunit': '1.1.1',
                    ]
            ]
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.tools.gradle}"
        classpath "org.jacoco:org.jacoco.core:${versions.tools.jacoco}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.tools.kotlin}"
        classpath "io.fabric.tools:gradle:${versions.tools.fabric}"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${versions.tools.spotbugs}"
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.4"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://jitpack.io'
        }
        mavenCentral()
    }

    project.extensions.extraProperties.set('SpotBugsTask', com.github.spotbugs.SpotBugsTask)
    apply from: "$project.rootDir/scripts/spotbugs.gradle"
    apply from: "$project.rootDir/scripts/checkstyle.gradle"
    apply from: "$project.rootDir/scripts/pmd.gradle"
    apply plugin: "com.dorongold.task-tree"
}

subprojects {
    afterEvaluate({
        if (project.plugins.hasPlugin(com.android.build.gradle.api.AndroidBasePlugin)) {
            if (project.tasks.stream().noneMatch({ t -> t.name.equals('jacocoTestReport') })) {
                project.apply from: "$project.rootDir/scripts/jacoco.gradle"
            }
        } else {
            project.apply plugin: 'jacoco'
            jacocoTestReport {
                reports {
                    xml.enabled = true
                    html.enabled = true
                }

                sourceDirectories = files(["$project.projectDir/src/main/java"])
                classDirectories = files([fileTree(dir: "$project.buildDir/classes")])
                executionData = fileTree(dir: "$project.buildDir/jacoco")
            }
        }

        project.tasks.check.dependsOn 'checkstyle', 'spotbugs', 'pmd'
    })

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
